plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'dev.hireben.demo'
version = 'SNAPSHOT'
description = 'Demo Spring Boot common library project'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}
 
repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.7')

  compileOnly 'org.springframework:spring-webmvc'
  compileOnly 'jakarta.servlet:jakarta.servlet-api'
  compileOnly 'jakarta.validation:jakarta.validation-api'
  compileOnly 'com.fasterxml.jackson.core:jackson-databind'
  compileOnly 'org.slf4j:slf4j-api'
  compileOnly 'io.micrometer:micrometer-tracing-bridge-brave'

  api 'io.jsonwebtoken:jjwt-api:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

  annotationProcessor 'org.projectlombok:lombok:1.18.40'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

publishing {
  publications {
		maven(MavenPublication) {
			from components.java
		}
	}
	repositories {
		mavenLocal()
	}
}

tasks.named('test') {
  useJUnitPlatform()
}

tasks.register('install') {
  dependsOn 'publishToMavenLocal'
  description = 'Install library to local .m2 repository'
  group = 'publishing'
}
